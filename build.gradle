buildscript {
    repositories {
        maven {
            url 'http://artifactory.itszuvalex.com/plugins-release'
            credentials {
                username = System.getenv("ARTIFACTORY_USER")
                password = System.getenv("ARTIFACTORY_PASS")
            }
        }
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

plugins {
    id "com.github.maiflai.scalatest" version "0.11"
}

allprojects{
    repositories {
        maven {
            url 'http://artifactory.itszuvalex.com/plugins-release'
            credentials {
                username = System.getenv("ARTIFACTORY_USER")
                password = System.getenv("ARTIFACTORY_PASS")
            }
        }
    }
}

dependencies {
    testCompile 'org.scalatest:scalatest_2.11:2+'
    testCompile 'org.scalamock:scalamock-scalatest-support_2.11:3.2.2'
    testRuntime 'org.pegdown:pegdown:1.5.0'
}



apply plugin: 'scala'
apply plugin: 'forge'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

project.ext {
    massive = "0"
    major = "1"
    minor = "0"
    mcVersion = "1.7.10"
}

version = "${project.ext.mcVersion}-${project.ext.massive}.${project.ext.major}.${project.minor}"

if (System.getenv().CI_BUILD_ID != null) {
    version += "-${System.getenv().CI_BUILD_ID}"
} else {
    version += "-local"
}

if (System.getenv().CI_BUILD_REF_NAME != null && !System.getenv().CI_BUILD_REF_NAME.equalsIgnoreCase("master")) {
    version += "-dev"
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}
compileScala {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
}

group = "com.itszuvalex.itszulib" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "itszulib"

minecraft {
    version = "${project.ext.mcVersion}-10.13.4.1448-${project.ext.mcVersion}"
    runDir = "eclipse"
// replacing stuff in the source
    replace '@MASSIVE@', project.massive
    replace '@MAJOR@', project.major
    replace '@MINOR@', project.minor
    replace '@MC_VERSION@', version
    if (System.getenv("CI_BUILD_ID") != null) {
        replace '@BUILD_NUMBER@', System.getenv("CI_BUILD_ID")
    } else {
        replace '@BUILD_NUMBER@', 0
    }
}

dependencies {
    compile 'org.scala-lang:scala-library:2.11.8'

    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"

    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}


sourceSets {
    main {
        scala {
            srcDirs = ['src/main/scala', 'src/main/java']
        }
        java {
            srcDirs = []
        }
    }
    api {
        java {
            srcDir 'api'
        }
    }
}

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            ext.srcFiles = "$projectDir/src/main/resources"
            ext.destDir = "$projectDir/build/classes/main"

            // registering inputs & outputs allow gradle to skip if UP-TO-DATE
            inputs.files srcFiles
            outputs.dir destDir

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                // replace version and mcversion
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
                exclude 'api-readme.txt'
            }
        }

//Begin I don't know what I'm doing

//Create API library jar
//Must be fully qualified name or it attempts to match against scala.io Jar
task apiJar(type: org.gradle.api.tasks.bundling.Jar) {
    baseName = "${archivesBaseName}-api"
    dependsOn check

    from(sourceSets.main.output) {
        include 'com/itszuvalex/itszulib/api/**'
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'api-readme.txt'
    }
}

task apiScalaDoc(type: org.gradle.api.tasks.scala.ScalaDoc) {
    include 'com/itszuvalex/itszulib/api/**.scala'
}

task jarApiScalaDoc(type: org.gradle.api.tasks.bundling.Jar) {
    baseName = "${archivesBaseName}-api-scaladoc"
    from apiScalaDoc
}

task debug << { println configurations.compile.findAll { it.name.startsWith("scala") } }

task getArtifacts(dependsOn: build) {
    outputs.files files(fileTree(dir: "$projectDir/build/libs").include('**/*.jar'))
}

//task getModJar << {
//    outputs.file file(fileTree(dir: "$projectDir/build/libs", include: '**/*.jar', exclude: '**/*api*').iterator().next())
//}

//task readOutputs(dependsOn: assemble) {
//      outputs.files  jar.outputs.getFiles()
//}

task jarSource(type: org.gradle.api.tasks.bundling.Jar) {
    baseName = "${archivesBaseName}-src"
    dependsOn check
    from sourceSets.main.scala.srcDirs
}

task jarDeobf(type: org.gradle.api.tasks.bundling.Jar) {
    baseName = "${archivesBaseName}-deobf"
    dependsOn check

    from(sourceSets.main.output)
}

build.dependsOn apiJar

publish.dependsOn assemble

publishing {
    publications {
        mod(MavenPublication) {
            groupId group
            version project.version

            artifact jar.outputs.files.iterator().next()
        }
        api(MavenPublication) {
            groupId group
            version project.version

            artifact apiJar {
                classifier "api"
            }
        }
        src(MavenPublication) {
            groupId group
            version project.version

            artifact jarSource {
                classifier "src"
            }
        }
        deobf(MavenPublication) {
            groupId group
            version project.version

            artifact jarDeobf {
                classifier 'deobf'
            }
        }
        scalaDoc(MavenPublication) {
            groupId group
            version project.version

            artifact jarApiScalaDoc {
                classifier "api-scaladoc"
            }
        }
    }
}

artifactory {
    contextUrl = "http://artifactory.itszuvalex.com"
    publish {
        repository {
            repoKey = 'ItszuLib'
            username = System.getenv("ARTIFACTORY_USER")
            password = System.getenv("ARTIFACTORY_PASS")
            maven = true

        }
        defaults {
            publications ('mod', 'api', 'src', 'deobf', 'scalaDoc')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = System.getenv("ARTIFACTORY_USER")
            password = System.getenv("ARTIFACTORY_PASS")
            maven = true

        }
    }
}

idea {
    project {
        languageLevel = '1.7'
        vcs = 'Git'
    }
    module {
        inheritOutputDirs = true
    }
}
